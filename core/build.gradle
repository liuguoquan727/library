apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.github.dcendents.android-maven'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"

    consumerProguardFiles 'proguard.pro'

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    ndk {
      // 设置支持的SO库架构
      abiFilters 'armeabi-v7a'
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  api fileTree(dir: 'libs', include: ['*.jar'])
  androidTestApi('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  api "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
  api "com.android.support:support-annotations:${SUPPORT_VERSION}"
  testApi 'junit:junit:4.12'


  api "com.google.code.gson:gson:${GSON_VERSION}"

  api project(':BaseRecyclerAdapter')
  api project(':core-base')
  api project(':dialogplus')

  // butterknife系列
  api("com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}") {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'support-compat'
  }
  annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
  // okhttp系列
  api "com.squareup.okhttp3:okhttp:${OK_HTTP_VERSION}"
  api "com.squareup.okhttp3:logging-interceptor:${OK_HTTP_VERSION}"

  // rxJava系列
  api "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
  api "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
  api("com.jakewharton.rxbinding2:rxbinding:${RXBINDING_VERSION}") {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
  }
  api("com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:${RXBINDING_SUPPORT_VERSION}") {
    exclude group: 'com.android.support', module: 'recyclerview-v7'
  }
  api("com.jakewharton.rxbinding2:rxbinding-support-v4:${RXBINDING_SUPPORT_VERSION}") {
    exclude group: 'com.android.support', module: 'support-core-ui'
  }

  // If you want to bind to Android-specific lifecycles
  api("com.trello.rxlifecycle2:rxlifecycle-android:${RXLIFECYCLE_VERSION}") {
    exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
  }
  // If you want to use Navi for providers
  api("com.trello.rxlifecycle2:rxlifecycle-navi:${RXLIFECYCLE_VERSION}") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  // glide系列
  api "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
  annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"
  api "jp.wasabeef:glide-transformations:${GLIDE_TRANSFORMATIONS_VERSION}"

  api("com.afollestad.material-dialogs:core:${MATERIAL_DIALOGS}") {
    exclude group: 'me.zhanghai.android.materialprogressbar', module: 'library'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'support-v13'
    exclude group: 'com.android.support', module: 'recyclerview-v7'
    exclude group: 'com.android.support', module: 'appcompat-v7'
  }
  api("me.zhanghai.android.materialprogressbar:library:1.4.2") {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'appcompat-v7'
  }
  // 友盟数据统计
  api "com.umeng.analytics:analytics:6.1.3"
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  failOnError false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}
// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
